DeclareClass Name=Script
DeclareClass Name=Pawn
DeclareClass Name=Class
DeclareClass Name=Light
DeclareClass Name=Satellite
DeclareClass Name=TorchFlame
DeclareClass Name=Spotlight
DeclareClass Name=WeaponLight
DeclareClass Name=SightLight
DeclareClass Name=TriggerLight

Begin Class Name=Light
   Begin Text
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//=============================================================================
// The light class.
//=============================================================================
class Light expands Actor
	package(UnEngine);

#exec Texture Import File=..\Graphics\S_Light.pcx  Name=S_Light Palette=Palette Mips=Off Flags=2
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   End Text
   Begin DefaultProperties
      bStatic=True
      bHidden=True
      Texture=S_Light
      CollisionRadius=+00024.000000
      CollisionHeight=+00024.000000
      LightType=LT_Steady
      LightBrightness=64
      LightSaturation=255
      LightRadius=64
      LightPeriod=32
      LightCone=128
      VolumeBrightness=128
      VolumeRadius=16
      DefaultEdCategory=Lighting
   End DefaultProperties
End Class

Begin Class Name=Satellite
   Begin Text
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//=============================================================================
// Satellite, a special kind of light which appears in the backdrops
// and rises/falls according to the level's day/night cycle.
//=============================================================================
class Satellite expands Light
	package(UnEngine);

//-----------------------------------------------------------------------------
// Variables.

// Amount that level's day cycle is scaled by, normally 1.0.
var() float Period;

// Phase offset from start of level time, 0.0 to 1.0.
var() float Phase;

var vector SatX,SatY,SatZ;

//-----------------------------------------------------------------------------
// Functions.

//
// Remember our coordinate system.
//
function BeginPlay()
{
	GetUnAxes(Rotation,SatX,SatY,SatZ);
}

function Tick( float DeltaTime )
{
	SetLocation
	(	sin(Level.TimeSeconds) * SatZ
	+	cos(Level.TimeSeconds) * SatX);
}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   End Text
   Begin DefaultProperties
      Period=+00001.000000
      Phase=+00000.000000
      SatX=(+00000.000000,+00000.000000,+00000.000000)
      SatY=(+00000.000000,+00000.000000,+00000.000000)
      SatZ=(+00000.000000,+00000.000000,+00000.000000)
      bStatic=False
      bDirectional=True
      LightEffect=LE_Satellite
      LightBrightness=128
      bOnHorizon=True
   End DefaultProperties
End Class

Begin Class Name=TorchFlame
   Begin Text
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//=============================================================================
// Torchflame.
//=============================================================================
class TorchFlame expands Light
	package(UnGame);

#exec MESH IMPORT MESH=Flame ANIVFILE=..\MODELS\aniv56.3D DATAFILE=..\MODELS\data56.3D X=0 Y=0 Z=0
#exec MESH ORIGIN MESH=Flame X=0 Y=100 Z=350 YAW=0
#exec MESH SEQUENCE MESH=Flame SEQ=All    STARTFRAME=0  NUMFRAMES=12
#exec MESH SEQUENCE MESH=Flame SEQ=Burn   STARTFRAME=0  NUMFRAMES=12
#exec TEXTURE IMPORT NAME=JFlame1 FILE=..\MODELS\fire.PCX FAMILY="Skins" PALETTE=JFlame1
#exec MESHMAP NEW   MESHMAP=Flame MESH=Flame
#exec MESHMAP SCALE MESHMAP=Flame X=0.015 Y=0.015 Z=0.03
#exec MESHMAP SETTEXTURE MESHMAP=Flame NUM=1 TEXTURE=JFlame1


Auto State Flicker
{
Begin:
	LoopAnim('Burn');
}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   End Text
   Begin DefaultProperties
      bStatic=False
      bHidden=False
      DrawType=DT_Mesh
      Mesh=Flame
      LightEffect=LE_FireWaver
      LightBrightness=40
      LightRadius=32
      bUnlit=True
      AnimRate=+00001.000000
   End DefaultProperties
End Class

Begin Class Name=Spotlight
   Begin Text
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//=============================================================================
// A directional spotlight.
//=============================================================================
class Spotlight expands Light
	package(UnEngine);
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   End Text
   Begin DefaultProperties
      bDirectional=True
      LightEffect=LE_Spotlight
   End DefaultProperties
End Class

Begin Class Name=WeaponLight
   Begin Text
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//=============================================================================
// WeaponLight.
//=============================================================================
class WeaponLight expands Light
	package(UnGame);

auto state Fade
{
Begin:
	LightBrightness = 100;
}

function Tick( float DeltaTime )
{
	LightBrightness -= 50;
	If (LightBrightness == 0) Destroy();

}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   End Text
   Begin DefaultProperties
      LightBrightness=127
      LightRadius=6
      bActorShadows=True
   End DefaultProperties
End Class

Begin Class Name=SightLight
   Begin Text
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//=============================================================================
// SightLight.
//=============================================================================
class SightLight expands Light
	package(UnGame);

#exec Texture Import File=..\Graphics\Dot.pcx Name=s_Dot Palette=Palette Mips=Off Mask=On Flags=2
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   End Text
   Begin DefaultProperties
      LightBrightness=147
      LightRadius=32
      bActorShadows=True
   End DefaultProperties
End Class

Begin Class Name=TriggerLight
   Begin Text
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//=============================================================================
// TriggerLight.
// A lightsource which can be triggered on or off.
//=============================================================================
class TriggerLight expands Light
	package(UnEngine);

//-----------------------------------------------------------------------------
// Variables.

var() float ChangeTime;        // Time light takes to change from on to off.
var() bool  bInitiallyOn;      // Whether it's initially on.

var   ELightType InitialType;  // Initial type of light.
var   float InitialBrightness; // Initial brightness.
var   float Alpha, Direction;
var   actor Trigger;

//-----------------------------------------------------------------------------
// Engine functions.

// Called at start of gameplay.
function BeginPlay()
{
	// Remember initial light type and set new one.
	Disable( 'Tick' );
	InitialType       = LightType;
	InitialBrightness = LightBrightness;
	if( bInitiallyOn )
	{
		Alpha     = 1.0;
		Direction = 1.0;
	}
	else
	{
		LightType = LT_None;
		Alpha     = 0.0;
		Direction = -1.0;
	}
}

// Called whenever time passes.
function Tick( float DeltaTime )
{
	LightType = InitialType;
	Alpha += Direction * DeltaTime / ChangeTime;
	if( Alpha > 1.0 )
	{
		Alpha = 1.0;
		Disable( 'Tick' );
		if( Trigger != None )
			Trigger.ResetTrigger();
	}
	else if( Alpha < 0.0 )
	{
		Alpha = 0.0;
		Disable( 'Tick' );
		LightType = LT_None;
		if( Trigger != None )
			Trigger.ResetTrigger();
	}
	LightBrightness = Alpha * InitialBrightness;
}

//-----------------------------------------------------------------------------
// Public states.

// Trigger turns the light on.
state() TriggerTurnsOn
{
	function Trigger( actor Other, pawn EventInstigator )
	{
		Trigger   = None;
		Direction = 1.0;
		Enable( 'Tick' );
	}
}

// Trigger turns the light off.
state() TriggerTurnsOff
{
	function Trigger( actor Other, pawn EventInstigator )
	{
		Trigger   = None;
		Direction = -1.0;
		Enable( 'Tick' );
	}
}

// Trigger toggles the light.
state() TriggerToggle
{
	function Trigger( actor Other, pawn EventInstigator )
	{
		log("Toggle");
		Trigger   = Other;
		Direction *= -1;
		Enable( 'Tick' );
	}
}

// Trigger controls the light.
state() TriggerControl
{
	function Trigger( actor Other, pawn EventInstigator )
	{
		Trigger   = Other;
		if( bInitiallyOn ) Direction = -1.0;
		else               Direction = 1.0;
		Enable( 'Tick' );
	}
	function UnTrigger( actor Other, pawn EventInstigator )
	{
		Trigger   = Other;
		if( bInitiallyOn ) Direction = 1.0;
		else               Direction = -1.0;
		Enable( 'Tick' );
	}
}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   End Text
   Begin DefaultProperties
      ChangeTime=+00000.000000
      bInitiallyOn=False
      InitialType=LT_None
      InitialBrightness=+00000.000000
      Alpha=+00000.000000
      Direction=+00000.000000
      Trigger=None
      bStatic=False
   End DefaultProperties
End Class

